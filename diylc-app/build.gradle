plugins {
    id 'application'
    id "edu.sc.seis.macAppBundle" version "2.1.3"
}

mainClassName = "${mainClass}"
applicationDefaultJvmArgs = [
    "-Xms${minMemory}",
    "-Xmx${maxMemory}",
    "-Dorg.diylc.scriptRun=${scriptRun}"
]

macAppBundle {
    appName = 'DIYLC4'
    jvmVersion = '1.8+'
    mainClassName = "${mainClass}"
    bundleJRE = false
    javaProperties.put("org.diylc.scriptRun", "${scriptRun}")
    javaProperties.put("org.diylc.componentLibrary", "\$APP_ROOT/Contents/Resources/components")
    javaExtras.put("-Xms${minMemory}", null)
    javaExtras.put("-Xmx${maxMemory}", null)
    javaExtras.put("-splash:\$APP_ROOT/splash.png", null)
}


task copySplashImage(type: Copy) {
    from(file('src/main/resources/images'))
    into(file("build/macApp/DIYLC4.app"))
    include 'splash.png'
}

task copyComponents(type: Copy) {
    from(project(':diylc-component-library').file('src/main/groovy'))
    into(file("build/macApp/DIYLC4.app/Contents/Resources/components"))
    include '**/*.groovy'
}

task addResources(dependsOn: [copySplashImage, copyComponents]) {
    
}

createApp.finalizedBy addResources


jar {
    manifest {
        attributes("SplashScreen-Image": "images/splash.png")
    }
}

dependencies {
	compile(group: 'org.netbeans.api', name: 'org-openide-awt', version: 'RELEASE81')
	compile(group: 'org.netbeans.api', name: 'org-openide-util', version: 'RELEASE81')
	compile(group: 'org.netbeans.api', name: 'org-openide-util-lookup', version: 'RELEASE81')
}